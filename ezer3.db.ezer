#pragma library

// Systém Ezer - knihovní modul DB pro zobrazení struktury a obsahu databáze
// (c) 2021 Martin Šmídek <martin@smidek.eu>

# ===================================================================================== DATABÁZE
func onfirstfocus() { var db_info:object
  db_info= php.sys_db_info(this.par,this.self()); 
  mdb.str.attach_code(db_info.group);
}
func append(x) { info.append(x) }
func tab_append(tab,cond,mode) {
  echo(`tab_append(${tab},${cond},${mode}) ... ezer3.2`);
  clear();
  if (mode==1)
    info.append(php.sys_db_append_using(tab,cond));
  elseif (mode==2)
    info.append(php.sys_db_append(tab,`TRUE`));
//  elseif (mode==3)
//    info.append(php.man_append(tab,cond));
  else // mode==0
    info.append(php.sys_db_append(tab,cond));
}
func tab_id_show(tab,idt) {
  panel.focus();
  info.idt= idt;
  info.tab= tab;
  info.go.onclick();
}
func tab_rec_show(tab,key_name,tid) {
  echo(`tab_rec_show(${tab},${key_name},${tid}) ... ezer3.2`);
  clear();
  info.stick(php.sys_db_rec_show(tab,key_name,tid));
}
use info: form right [12,4,,] 
menu mdb {type:'left', active:*, format:'f+'
  menu  {title:'Zobrazení dat aplikace', type:'group', skill:'m'
    item {title:'[fa-question] zobrazení záznamů'
      func onclick (i:ezer) { 
        clear(); info.header(i); info.display(1,'t');
        if (!info.tab) {
          info.tab.selects(php.sys_db_selects());
          info.tab.key(1);
        }
      }
    }
  }
  menu str {title:'Struktura dat aplikace', type:'group'
    // itemy budou doplněny v onstart metodou attach_code
    func onclick (i:ezer) {
      clear(); info.header(i); info.display(0,'t');
      info.append(i.par.tab ? php.sys_db_struct(i.par.tab,0) : i.par.info);
    }
  }
  menu  {title:'Struktura dat jádra', type:'group', skill:'m'
    item {title:'[fa-database] tabulka _CIS', par:{tab:'_cis'} }
    func onclick (i:ezer) {
      clear(); info.header(i); info.display(0,'t');
      info.append(php.sys_db_struct(i.par.tab,0));
    }
  }
}
# ==> formulář pro levostranné menu s postupným zápisem a obsluhou zobrazení tabulek
form right [,,*,50] {
  var last_stick= 0 // délka před přilepení sticku
  label head [0,0,*,50]  { title:' ' }
  label note [0,36,*,] { title:' ' }
  func header(i:ezer) { var i_owner:ezer
    clear_stick();
    i_owner= i.owner();
    note= '';
    head= replace_fa(`<div class='karta'>${i_owner.title} - ${i.title}</div>`);
  }
  func fill(h,n) {
    clear_stick();
    if (h) head= conc("<div class='karta'>",h,"</div>");
    if (n) note= n;
  }
  func clear_stick() { 
    if (last_stick) {
      note= substr(note,0,last_stick);
    }
    last_stick= 0;
  }
  func stick(n) { 
    clear_stick();
    last_stick= array_length(note);
    note= conc(note,n);
  }
  func append(n) {
    clear_stick();
    note= conc(note,n);
  }
  select tab [-253,5,150,] {tag:'t', title:'tabulka', css:'v_karte', format:'t', skill:'m|m'}
  field idt [-152,5,70,] {tag:'t', title:'klíč', css:'v_karte', format:'t', skill:'m|m'}
  button go [-82,5,,] {tag:'t', title:'zobraz', skill:'m|m'
    func onclick() {
      clear(); form.note= conc(form.note,
          php.sys_db_append(tab,idt ? `*='${idt}'`:'TRUE')); // * bude primary_key
  }}
  button [-22,5,,] {tag:'t', title:'vymaž', skill:'m|m'
    func onclick() { clear(); form.note= ''; form.clear_stick();
  }}
}
